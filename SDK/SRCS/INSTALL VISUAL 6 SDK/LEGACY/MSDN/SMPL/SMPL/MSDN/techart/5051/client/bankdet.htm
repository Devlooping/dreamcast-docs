<html>
<head>
<title>Banking Details Page</title>
</head>
<body>
<script language="VBScript">
<!--
Dim oBank
Dim oAcct
Dim cPos
Dim cNeg
Dim iBank
Dim iAcct
Dim sBuff

'clear the account detail frame
top.acctdet.location.href = "empty.htm"
'create a local bank object 
iBank = top.banks.m_iCurrBank
Set oBank = top.banks.m_oAdmin.Banks(ibank)
'walk oBank.Accounts, write account list to accounts frame and build bank detail totals
top.accounts.document.open
top.accounts.document.writeln("<strong>Accounts</strong>")
top.accounts.document.writeln("<hr>")
For iAcct = 1 to oBank.Accounts.Count
    'create an HTML anchor representing account 
    Set oAcct = oBank.Accounts(iAcct)
    sBuff = "<a href=""detact.htm"" target=""acctdet"" "
    sBuff = sBuff & "onClick=""top.banks.m_iCurrAccount = " & CStr(iAcct) & """"
    sBuff = sBuff & ">" & oAcct.Number & "</a><br>"
    top.accounts.document.writeln(sBuff)
    'add account balance to bank totals
    If oAcct.Balance > 0 Then
        cPos = cPos + oAcct.Balance
    Else
        cNeg = cNeg + oAcct.Balance
    End If
Next
Set oAcct = Nothing
top.accounts.document.close

'write bank detail to current frame
document.writeln("Bank    - " & oBank.Name & "<br>")
document.writeln("Code    - " & oBank.Code & "<br>")
document.writeln("Address - " & oBank.Address1 & "<br>")
document.writeln("          " & oBank.Address2 & "<br>")
document.writeln("City    - " & oBank.City & "<br>")
document.writeln("Type    - " & oBank.TypeId & "<br>")
document.writeln("Credits - " & FormatCurrency(cPos) & "<br>")
document.writeln("Debits  - " & FormatCurrency(cNeg) & "<br>")
document.writeln("Net Val - " & FormatCurrency(cPos + cNeg) & "<br>")
-->
</script>
</body>
</html>
